buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    maven {
        name = "CurseForge"
        url = "https://cursemaven.com/"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

sourceSets {
    api {
        compileClasspath += configurations.getByName("minecraft")
    }
    vanillaApi {
        java {
            setSrcDirs([ 'src/vanilla-api/java' ])
        }
        resources {
            setSrcDirs([ 'src/vanilla-api/resources' ])
        }

        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
        compileClasspath += configurations.getByName("minecraft")
    }
    vanilla {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
        compileClasspath += sourceSets.vanillaApi.output
        runtimeClasspath += sourceSets.vanillaApi.output
        compileClasspath += configurations.getByName("minecraft")
    }
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
        compileClasspath += sourceSets.vanilla.output
        runtimeClasspath += sourceSets.vanilla.output
        compileClasspath += sourceSets.vanillaApi.output
        runtimeClasspath += sourceSets.vanillaApi.output
    }
    test {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

version = (System.getenv().containsKey("Version") ? System.getenv("Version") : project.modVersion)
project.logger.lifecycle("Building for version: " + version)

group = modGroup
archivesBaseName = modBaseName

java.toolchain.languageVersion = JavaLanguageVersion.of("16")

minecraft {
    mappings channel: "${project.mcpChannel}", version: "${project.mcpVersion}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', ''

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                aequivaleo {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.vanilla
                    source sourceSets.vanillaApi
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', ''

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                aequivaleo {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.vanilla
                    source sourceSets.vanillaApi
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', ''

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', project.modBaseName, '--all', '--output', file('src/generated/resources/'), '--input', file('src/main/resources')

            mods {
                aequivaleo {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.vanilla
                    source sourceSets.vanillaApi
                }
            }
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${project.exactMinecraftVersion}-${project.forgeVersion}"

    // unit testing
    testImplementation 'junit:junit:4.13'
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.2'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.2'
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4-rule-agent', version: '2.0.2'

    testImplementation 'org.assertj:assertj-core:3.9.0'

    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'

    testRuntimeOnly group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version:'2.9.9'
    testRuntimeOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.10.0'

    //Utilities
    apiImplementation 'org.jetbrains:annotations:21.0.1'
    vanillaImplementation 'org.jetbrains:annotations:21.0.1'
    vanillaApiImplementation 'org.jetbrains:annotations:21.0.1'
    implementation "org.jetbrains:annotations:21.0.1"
    testImplementation 'org.jetbrains:annotations:21.0.1'

    implementation "org.jgrapht:jgrapht-core:${project.jgraphtVersion}"
    implementation "org.jgrapht:jgrapht-ext:${project.jgraphtVersion}"
    implementation "org.jgrapht:jgrapht-io:${project.jgraphtVersion}"
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier 'api'
    baseName project.name
}


task vanillaApiJar(type: Jar) {
    from sourceSets.vanillaApi.output
    classifier 'vanilla-api'
    baseName project.name
}

processResources {
}

jar {
    from sourceSets.api.output
    from sourceSets.vanillaApi.output
    from sourceSets.vanilla.output
    from sourceSets.main.output

    manifest {
        attributes([
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}:universal",
                'Timestamp': System.currentTimeMillis(),
                "Specification-Title": project.name.toLowerCase(),
                "Specification-Vendor": "ldtteam",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"ldtteam",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

javadoc {
    source += sourceSets.api.allSource
    source += sourceSets.vanillaApi.allSource
    source += sourceSets.vanilla.allSource

    configure( options ) {
        tags(
                'apiNote:a:API Note:',
                'implSpec:a:Implementation Requirements:',
                'implNote:a:Implementation Note:'
        )
    }
}

task packageJavadoc(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

task apiJavadocs(type: Javadoc) {
    source = sourceSets.api.allJava
    classpath = sourceSets.api.compileClasspath

    destinationDir = project.buildDir.toPath().resolve("docs/main/api").toFile()

    configure( options ) {
        tags(
                'apiNote:a:API Note:',
                'implSpec:a:Implementation Requirements:',
                'implNote:a:Implementation Note:'
        )
    }
}

task vanillaApiJavadocs(type: Javadoc) {
    source = sourceSets.vanillaApi.allJava
    classpath = sourceSets.vanillaApi.compileClasspath

    destinationDir = project.buildDir.toPath().resolve("docs/vanilla/api").toFile()

    configure( options ) {
        tags(
                'apiNote:a:API Note:',
                'implSpec:a:Implementation Requirements:',
                'implNote:a:Implementation Note:'
        )
    }
}

task packageApiJavadoc(type: Jar) {
    from apiJavadocs
    archiveClassifier.set('javadoc-api')
}

task packageVanillaApiJavadoc(type: Jar) {
    from vanillaApiJavadocs
    archiveClassifier.set('javadoc-vanilla-api')
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
    from sourceSets.api.allJava
    from sourceSets.vanillaApi.allJava
    from sourceSets.vanilla.allJava
}

task apiSourceJar(type: Jar) {
    archiveClassifier.set('api-sources')
    from sourceSets.api.allJava
}

task vanillaApiSourceJar(type: Jar){
    archiveClassifier.set('vanilla-api-sources')
    from sourceSets.vanillaApi.allJava
}

task setupDecompWorkspace {
    afterEvaluate {
        println "Setup"
    }
}

task setupCIWorkspace {
    afterEvaluate {
        println "Setup"
    }
}

shadowJar {
    from sourceSets.main.output
    from sourceSets.vanillaApi.output
    from sourceSets.vanilla.output
    from sourceSets.api.output

    // Only shadow apache jgrapht dependencies
    dependencies {
        include dependency("org.jgrapht:jgrapht-core:${project.jgraphtVersion}")
        include dependency("org.jgrapht:jgrapht-ext:${project.jgraphtVersion}")
        include dependency("org.jgrapht:jgrapht-io:${project.jgraphtVersion}")
    }
    // Relocate apache jgrapht to prevent conflicts with other mods that include it
    relocate 'org.jgrapht', 'com.ldtteam.shaded.jgrapht'

    archiveClassifier.set('universal') // Replace the default JAR
}


reobf {
    shadowJar {
    }
    apiJar {
    }
    vanillaApiJar {
    }
}


curseforge {
    if (System.getenv().CURSEAPIKEY != null && System.getenv().CURSERELEASETYPE != null)
    {
        apiKey = System.getenv().CURSEAPIKEY
        def additionalVersions = project.additionalMinecraftVersions.split(',')

        project {
            id = '404450'
            addGameVersion project.minecraftVersion

            additionalVersions.each {
                addGameVersion it.trim()
            }

            changelog = file('build/changelog.md')
            changelogType = 'markdown'
            releaseType = System.getenv().CURSERELEASETYPE

            addArtifact packageApiJavadoc
            addArtifact apiSourceJar
            addArtifact sourceJar
            addArtifact apiJar
            addArtifact packageJavadoc
            addArtifact packageVanillaApiJavadoc
            addArtifact vanillaApiSourceJar

            mainArtifact(shadowJar)
        }
    }
    else
    {
        logger.lifecycle("Cannot run the CurseUpload sequence. No API-Key or release type has been provided.")
    }
}

task("createChangelog") {
    group = 'upload'

    doLast {
        def teamCityURL = "https://buildsystem.ldtteam.com/"
        def file = new FileOutputStream("build/changelog.md")
        def out = new BufferedOutputStream(file)
        def changesXML = new XmlSlurper().parse(teamCityURL + "guestAuth/app/rest/changes?locator=build:(id:" + teamcity["teamcity.build.id"] + ")")

        def changes = changesXML.change
        logger.lifecycle("Identified " + changes.size() + " changes to be written into the changelog.")

        out << "# Aequivaleo Changelog \n"
        out << "## Version: _" + version + "_ \n"

        if (changes.size() > 0) {
            for (int i = 0; i < changes.size(); i++) {
                def changeDetailsURL = teamCityURL + "guestAuth/app/rest/changes/id:" + changes[i].@id.text()
                def changeDetailsXml = new XmlSlurper().parse(changeDetailsURL)
                def changeComment = changeDetailsXml.comment.text().trim()

                out << "* " + changeComment + "\n"
            }
        } else {
            out << "No Changes detected!"
        }

        out.close()
    }
}

artifacts {
    shadowJar
    packageJavadoc
    sourceJar
    apiJar
    packageApiJavadoc
    apiSourceJar
    vanillaApiJar
    vanillaApiSourceJar
    packageVanillaApiJavadoc
}

tasks.build.dependsOn tasks.shadowJar
tasks.build.dependsOn tasks.packageJavadoc
tasks.build.dependsOn tasks.sourceJar
tasks.build.dependsOn tasks.apiJar
tasks.build.dependsOn tasks.packageApiJavadoc
tasks.build.dependsOn tasks.apiSourceJar
tasks.build.dependsOn tasks.vanillaApiJar
tasks.build.dependsOn tasks.vanillaApiSourceJar
tasks.build.dependsOn tasks.packageVanillaApiJavadoc

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group

            from components.java
            artifact shadowJar
            artifact packageJavadoc
            artifact sourceJar
            artifact apiJar
            artifact packageApiJavadoc
            artifact apiSourceJar
            artifact vanillaApiJar
            artifact packageVanillaApiJavadoc
            artifact vanillaApiSourceJar

            pom {
                url = 'https://github.com/ldtteam/Aequivaleo'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'OrionDevelopment'
                        name = 'Marc Hermans'
                        email = 'marc.hermans@ldtteam.com'
                    }
                }
                contributors {
                    contributor {
                        id = 'ldtteam'
                        name = 'Let\'s Develop Together - Team'
                        email = 'info@ldtteam.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ldtteam/aequivaleo.git'
                    developerConnection = 'scm:git:ssh://github.com/ldtteam/aequivaleo.git'
                    url = 'https://github.com/ldtteam/Aequivaleo'
                }
            }

            //Replace all deobf dependencies with normal jar references.
            pom.withXml { xmlProvider ->
                NodeList dependencies = asNode().dependencies
                NodeList allDeps = dependencies.'*'

                // Remove forge deps
                allDeps.findAll() { Node el ->
                    (el.artifactId.text() == 'forge' && el.groupId.text() == 'net.minecraftforge' || el.groupId.text() == 'curse.maven')
                }.forEach() { Node el ->
                    el.parent().remove(el)
                }
                //remove forgegradle's mapped suffix from versions & set as optional so anyone else doesn't inherit them
                allDeps.findAll() { Node el ->
                    el.version.text().contains('_mapped_')
                }.each { Node el ->
                    NodeList version = el.version
                    version.each {
                        it.setValue(it.text().substring(0, it.text().indexOf('_mapped_')))
                    }
                }
            }
        }
    }

    if (System.getenv().containsKey("LDTTeamJfrogUsername") && System.getenv().containsKey("LDTTeamJfrogPassword")) {
        project.logger.log(LogLevel.WARN, "Uploading to JFrog enabled.")
        repositories {
            maven {
                name 'LDTTeamJfrog'
                credentials {
                    username System.getenv().get("LDTTeamJfrogUsername")
                    password System.getenv().get("LDTTeamJfrogPassword")
                }
                url 'https://ldtteam.jfrog.io/ldtteam/mods-maven'
            }
        }
    }
    else {
        project.logger.log(LogLevel.WARN, "Uploading to JFrog disabeled.")
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

test {
    doFirst {
        jvmArgs = [
                '--add-opens', 'java.base/java.util.stream=ALL-UNNAMED',
                '--add-opens', 'java.base/java.io=ALL-UNNAMED',
                '--add-opens', 'java.xml/jdk.xml.internal=ALL-UNNAMED'
        ]
    }
}


afterEvaluate {
    tasks.processApiResources.duplicatesStrategy = DuplicatesStrategy.WARN
    tasks.processVanillaResources.duplicatesStrategy = DuplicatesStrategy.WARN
    tasks.processVanillaApiResources.duplicatesStrategy = DuplicatesStrategy.WARN
    tasks.processTestResources.duplicatesStrategy = DuplicatesStrategy.WARN
}